-- Verifiable Elections Electronic Pollbook System (Digital Voter List)
-- Copyright (C) 2015 Verifiable Elections

-- ===================================================================
-- Cluster CRYPTOGRAPHY
-- ===================================================================

class_chart CIPHER 
  explanation 
    "An entity that encrypts and decrypts data. This class is meant to generally \
   \ describe a large number of possible cryptographic implementations, such as \
   \ stream ciphers and block cipher modes."
  query
    "Is the_ciphertext a valid ciphertext for your algorithm?",
    "Is the_key a valid encryption key for your algorithm?",
    "Is the_key a valid decryption key for your algorithm?",
    "Is the_initialization_vector a valid initialization vector for your algorithm?",
    "What is the plaintext that corresponds to the decryption of the_ciphertext \
   \ using the_decryption_key and the_initialization_vector?",
    "What is the ciphertext that corresponds to the encryption of the_plaintext \
   \ using the_encryption_key and the_initialization_vector?"
  constraint
    "Plaintext can only be requested for valid ciphertext using a valid key \
   \ and a valid initialization vector.",
    "If ciphertext c is generated by encrypting plaintext p using key ke and \
   \ initialization vector iv, then plaintext q generated by decrypting ciphertext \
   \ c using key kd and initialization vector iv is identical to plaintext p if \
   \ ke and kd are corresponding encryption and decryption keys (a public/private \
   \ key pair, or the same symmetric key).",
    "All generated ciphertext is valid."
end

class_chart CIPHERTEXT 
  explanation 
    "Encrypted data."
  query
    "What is your raw data?"
  command
    "Your raw data is the_data!"
end

class_chart HASH_FUNCTION 
  explanation 
    "A function that generates a hash code from provided data. This class \
   \ is meant to describe both keyed and non-keyed hashes."
  query
    "What is the hash of the_data?",
    "Is the_key a valid key for your algorithm?",
    "Is the_initialization_vector a valid initialization vector for your algorithm?",
    "What is the hash of the_data using the_key and the_initialization_vector?"
end

class_chart KEY 
  explanation 
    "A piece of information used to encrypt, decrypt, or hash data."
  query
    "What is your raw data?"
  command
    "Your raw data is the_data!"
end

class_chart PLAINTEXT 
  explanation 
    "Unencrypted data."
  query
    "What is your raw data?"
  command
    "Your raw data is the_data!"
end

class_chart PRIVATE_KEY 
  explanation 
    "A cryptographic key that is assumed to be secret."
  inherit KEY
  query
    "What is the public key that corresponds to you?"
  command
    "The public key that corresponds to you is the_key!"
end

class_chart PUBLIC_KEY 
  explanation 
    "A cryptographic key that is assumed to be publicly known."
  inherit KEY
end
    
class_chart SIGNATURE
  explanation
    "An entity that handles digital signature generation and verification."
  query
    "Can the_signature of the_data be verified using the_verification_key?",
    "What is the signature of the_data using the_signing_key?"
  constraint
    "If signature s is generated by signing data d using key ks, then \
   \ signature s of data d can be verified using key kv if ks and \
   \ kv are corresponding signing and verificaiton keys."
end
